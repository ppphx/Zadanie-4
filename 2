// Параметры модели
R  = 0.03;        // рождаемость "жертв"
P1 = 1.5e-6;      // смертность "жертв" от "хищников"
P2 = 1e-6;        // прирост "хищников" за счёт "жертв"
D  = 0.02;        // естественная смертность "хищников"

// Равновесная точка
x_star = D / P2;  // 20 000
y_star = R / P1;  // 20 000

// Временной интервал
t0 = 0;
t_end = 3000;
dt = 5;
t = t0:dt:t_end;

// Числовые начальные условия
// Каждая строка: [x0, y0]
IC = [
    1e6,    1000;   // 1) много жертв
    1000,   1e5;    // 2) много хищников
    1e4,    10;     // 3) почти нет хищников
    x_star, y_star; // 4) в равновесии
    5e4,    5e3     // 5) обычный сценарий
];

// Описания
descriptions = [
    "1) x0 = 1 млн (много жертв)";
    "2) y0 = 100 тыс (много хищников)";
    "3) y0 очень мало (почти вымирание хищников)";
    "4) Начало в равновесии";
    "5) Обычный сценарий"
];

// Ф-ия модели
function dXdt = lotka_volterra(t, X)
    x = X(1);
    y = X(2);
    dxdt = R * x - P1 * x * y;
    dydt = P2 * x * y - D * y;
    dXdt = [dxdt; dydt];
endfunction

// Цикл
for i = 1:size(IC, 1)
    x0 = IC(i, 1);
    y0 = IC(i, 2);
    descr = descriptions(i);
    
    X0 = [x0; y0];
    
    // Численное решение
    X = ode(X0, t0, t, lotka_volterra);
    x = X(1, :);
    y = X(2, :);
    
    // График
    figure(2*i - 1);
    clf();
    plot(t, x, 'b-', 'LineWidth', 1.5);
    plot(t, y, 'r--', 'LineWidth', 1.5);
    xlabel('Время');
    ylabel('Численность');
    title(descr);
    xgrid();
    legend(['Жертвы (x)', 'Хищники (y)'], 'in_upper_right');
    

end

// Вывод
mprintf("Параметры модели:\n");
mprintf("  R  = %.4f\n", R);
mprintf("  P1 = %.2e\n", P1);
mprintf("  P2 = %.2e\n", P2);
mprintf("  D  = %.4f\n", D);
mprintf("\nРавновесие: x* = %g, y* = %g\n", x_star, y_star);
